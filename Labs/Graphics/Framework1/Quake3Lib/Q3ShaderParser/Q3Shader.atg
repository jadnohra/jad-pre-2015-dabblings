/******************************************************************************
* http://www.ssw.uni-linz.ac.at/Research/Projects/Coco/Contributions/
* Modified version of: "Grammar for Q3 shader scripts. Thanks to Stephan Reiter!"
******************************************************************************/

COMPILER Q3Shader
IGNORECASE

CHARACTERS
  letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz".
  digit = "0123456789".
  cr  = '\r'.
  lf  = '\n'.
  tab = '\t'.

TOKENS
  path  = letter { letter | digit | '/' | '_' | '.' | '-' }.
  number = [ '-' ] digit { digit } [ '.' { digit } ].

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO cr lf

IGNORE cr + lf + tab

PRODUCTIONS

Path
=
  path
.

Number
=
  number
.

GeneratorFunctionType
=
  ( "sin"
  | "triangle"
  | "square"
  | "sawtooth"
  | "inversesawtooth"
  | "random"
  | "noise"
  )
.

GeneratorFunction
=
  GeneratorFunctionType Number /*base*/ Number /*amplitude*/ Number /*phase*/ Number /*frequency*/
.

Q3Map2Func
=
  ( "dotProduct" '(' Number Number Number ')'
  | "dotProduct2" '(' Number Number Number ')'
  | "dotProductScale" '(' Number Number Number Number Number')'
  | "dotProduct2Scale" '(' Number Number Number Number Number')'
  | "scale" Number
  | "set" Number
  )
.

Q3Map2TcGenFunc
=
  ( "vector" Number Number
  | "ivector" Number Number
  )
.

Q3Map2TcModFunc
=
  ( "rotate"
    Number /*degrees*/
  | "scale"
    Number /*scale.s*/ Number /*scale.t*/
  | ( "translate" | "shift" | "move" )
    Number Number 
  )
.

BlendMode
=
  ( "GL_ZERO"
  | "GL_ONE"
  | "GL_SRC_COLOR"
  | "GL_ONE_MINUS_SRC_COLOR"
  | "GL_DST_COLOR"
  | "GL_ONE_MINUS_DST_COLOR"
  | "GL_SRC_ALPHA"
  | "GL_ONE_MINUS_SRC_ALPHA"
  | "GL_DST_ALPHA"
  | "GL_ONE_MINUS_DST_ALPHA"
  )
.

RgbGen
=
  "rgbGen"
  ( "identityLighting"
  | "identity"
  | "entity"
  | "oneMinusEntity"
  | "vertex"
  | "oneMinusVertex"
  | "exactVertex"
  | "lightingDiffuse"
  | "lightingSpecular"
  | "wave"
    GeneratorFunction
  | "const"
    '(' Number /*color.x*/ Number /*color.y*/ Number /*color.z*/ ')'
  )
.

AlphaGen
=
  "alphaGen"
  ( "identityLighting"
  | "identity"
  | "entity"
  | "oneMinusEntity"
  | "vertex"
  | "oneMinusVertex"
  | "exactVertex"
  | "lightingDiffuse"
  | "lightingSpecular"
  | "wave"
    GeneratorFunction
  | "const"
    Number /*alpha*/
  | "portal"
    Number /*range*/
  )
.

TcGen
=
  "tcGen"
  ( "base"
  | "lightmap"
  | "environment"
  | "vector"
    '(' Number /*u.x*/ Number /*u.y*/ Number /*u.z*/ ')'
    '(' Number /*v.x*/ Number /*v.y*/ Number /*v.z*/ ')'
  )
.

TcMod
=
  "tcMod"
  ( "rotate"
    Number /*rate*/
  | "scale"
    Number /*scale.x*/ Number /*scale.y*/
  | "scroll"
    Number /*rate.x*/ Number /*rate.y*/
  | "stretch"
    GeneratorFunction
  | "transform"
    Number /*u.x*/ Number /*u.y*/ Number /*v.x*/ Number/*v.y*/ Number /*o.x*/ Number /*o.y*/
  | "turb"
    Number /*base*/ Number /*amplitude*/ Number /*phase*/ Number /*frequency*/
  | "entityTranslate"
  )
.

AlphaFunc
=
  "alphaFunc"
  ( "GT0"
  | "LT128"
  | "GE128"
  ) .

Stage
=
  '{'
  { "map"
    ( "$lightmap"
    | ( "$whiteimage" | "*white" )
    | Path
    )
  | "clampmap" Path
  | "animMap" Number /*msecs*/
    Path
    { Path }
  | "blendFunc"
    ( "add"
    | "filter"
    | "blend"
    | BlendMode /*srcBlend*/ BlendMode /*destBlend*/
    )
  | RgbGen
  | AlphaGen
  | TcGen
  | TcMod
  | AlphaFunc
  | "depthFunc" ( "equal" | "lequal" | "disable" )
  | "depthWrite"
  | "detail"
  }
  '}'
.

SkyParms
=
  "skyParms"
  ( Path /*farBox*/ | '-' )
  Number /*cloudHeight*/
  ( Path /*nearBox*/ | '-' )
.

DeformVertexes
=
  "deformVertexes"
  ( "wave"
    Number /*div*/
  | "normal"
    Number /*div*/
    ( Number
    | GeneratorFunction
    )
  | "move"
    Number /*dir.x*/ Number /*dir.y*/ Number /*dir.z*/ GeneratorFunction
  | "bulge" Number Number Number
  | "autoSprite"
  | "autoSprite2"
  | "projectionShadow"
  )
.

FogParms
=
  "fogparms"
  '(' Number /*color.x*/ Number /*color.y*/ Number /*color.z*/ ')' Number /*opaqueDist*/
.

SurfaceParm
=
  "surfaceparm"
  ( "alphashadow" | "areaportal" | "clusterportal" | "donotenter" | "flesh" | "fog" | "lava"
  | "metalsteps" | "nodamage" | "nodlight" | "nodraw" | "nodrop" | "noimpact" | "nomarks"
  | "nolightmap" | "nosteps" | "nonsolid" | "origin" | "playerclip" | "slick" | "slime"
  | "structural" | "trans" | "water" | "pointlight" | "forcefield" | "shotclip" | "sky"
  | "monsterclip" | "hint" | "ladder" | "detail"
  )
.

ShaderDecl
=
  Path
  '{'
  { SkyParms
  | Stage
  | DeformVertexes
  | FogParms
  | "cull" ( "front" | "back" | "disable" | "none" )
  | "sort" ( Number | "portal" | "sky" | "opaque" | "banner" | "underwater" | "additive" | "nearest" )
  | "nopicmip"
  | "nomipmaps"
  | "polygonOffset"
  | "portal"
  | "entityMergable"
  | "tessSize" Number
  | "light" Number
  | SurfaceParm
  | "qer_editorimage" Path
  | "qer_nocarve"
  | "qer_trans" Number
  | "q3map_alphaGen" Q3Map2Func
  | "q3map_alphaMod" Q3Map2Func
  | "q3map_backshader" Path
  | "q3map_backsplash" Number Number
  | "q3map_baseshader" Path
  | "q3map_bounce" Number
  | "q3map_bounceScale" Number
  | "q3map_clipModel"
  | "q3map_cloneShader" Path
  | "q3map_colorGen" 
  | "q3map_colorMod" 
  | "q3map_fogDir" Number /*Angle*/
  | "q3map_forceMeta"
  | "q3map_forceSunlight"
  | "q3map_fur" Number /*layers*/ Number /*offset*/ Number /*fade*/
  | "q3map_globaltexture"
  | "q3map_indexed"
  | "q3map_invert"
  | "q3map_lightImage" Path
  | "q3map_lightmapAxis" Number /*axis*/
  | "q3map_lightmapBrightness" Number
  | "q3map_lightmapFilterRadius" Number /*self*/ Number /*other*/
  | "q3map_lightmapGamma" Number
  | "q3map_lightmapMergable"
  | "q3map_lightmapSampleOffset" Number /*distance*/
  | "q3map_lightmapSampleSize" Number
  | "q3map_lightmapSize" Number /*width*/ Number /*height*/
  | "q3map_lightRGB" Number /*red*/ Number /*green*/ Number /*blue*/
  | "q3map_lightStyle" Number
  | "q3map_lightSubdivide" Number
  | "q3map_noClip"
  | "q3map_noFast"
  | "q3map_noFog"
  | "q3map_nonPlanar"
  | "q3map_normalImage" Path
  | "q3map_noTJunc"
  | "q3map_noVertexLight" 
  | "q3map_noVertexShadows"
  | "q3map_offset" Number
  | "q3map_patchShadows"
  | "q3map_remapShader" Number
  | "q3map_replicate"
  | "q3map_rgbGen"
  | "q3map_rgbMod"
  | "q3map_shadeAngle" Number
  | "q3map_skylight" Number /*amount*/ Number /*iterations*/
  | "q3map_splotchFix"
  | "q3map_styleMarker"
  | "q3map_styleMarker2" 
  | "q3map_sun" Number /*red*/ Number /*green*/ Number /*blue*/ Number /*intensity*/ Number /*degrees*/ Number /*elevation*/
  | "q3map_sunExt" Number /*red*/ Number /*green*/ Number /*blue*/ Number /*intensity*/ Number /*degrees*/ Number /*elevation*/ Number /*deviance*/ Number /*samples*/
  | "q3map_sunlight"
  | "q3map_surfacelight" Number
  | "q3map_surfaceModel" Path /*modelpath*/ Number /*density*/ Number /*odds*/ Number /*minscale*/ Number /*maxscale*/ Number /*minangle*/ Number /*maxangle*/ Number /*oriented*/
  | "q3map_tcGen" Q3Map2TcGenFunc
  | "q3map_tcMod" Q3Map2TcModFunc
  | "q3map_terrain"
  | "q3map_tessSize" Number /*amount*/
  | "q3map_textureSize" Number /*X*/ Number /*Y*/
  | "q3map_traceLight"
  | "q3map_vertexScale" Number
  | "q3map_vertexShadows"
  | "q3map_vlight"
  }

  '}'

.

Q3Shader
=
  { ShaderDecl }
.

END Q3Shader.
